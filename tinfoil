#!/usr/bin/env zsh
#
# Tinfoil
#
# A tool to manage multiple profiles for web browsers
#
# {{{ License
#
# Copyright (C) 2017 Dyne.org Foundation
#
# Tinfoil is designed, written and maintained by Denis Roio <jaromil@dyne.org>
#
# This source code is free software; you can redistribute it and/or
# modify it under the terms of the GNU Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This source code is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  Please refer
# to the GNU Public License for more details.
#
# You should have received a copy of the GNU Public License along with
# this source code; if not, write to: Free Software Foundation, Inc.,
# 675 Mass Ave, Cambridge, MA 02139, USA.

# }}} - License

# {{{ Initialisation

# resources in order of priority
# this is where tinfoil looks for template/ and firejail/ dirs
homedir=$HOME/.tinfoil
# this can be changed, for instance to move templates in a tomb
profiledir=${TINFOIL_HOME:-$homedir/profiles}

confdir=/etc/tinfoil
# this is changed to $PREFIX/share/tinfoil by make install
basedir=.

source $basedir/zuper/zuper

# globals
vars+=(profiledir homedir confdir basedir)
vars+=(templatedir create_profile prefs profilepath)
vars+=(found here)
vars+=(firejail_profile)
maps+=(browser browsertype vendor profilename)
arrs+=(browser_opts)

source $basedir/zuper/zuper.init

vendor=(firefox     "mozilla/firefox"
		firefox-esr "mozilla/firefox"
		icecat      "mozilla/icecat"
		palemoon    "moonchild productions/pale moon")

here=$(pwd)

# }}}

# {{{ Launchers

function launch-chrome() {
	fn launch-chrome $*
    profilepath="${profiledir}/${browser}.${profile}"
	req=(browser profile)
	freq=($profilepath)
	ckreq || return $?

	act "launching     : $browser"
	act "profile path  : $profilepath"

	$app --user-data-dir="$profilepath" ${=browser_opts} $*
}

function launch-mozilla() {
	fn launch-mozilla $*
	func "browser: $browser"
	func "profile: $profile"
    profilepath="${profiledir}/${browser}.${profile}"
	local vendordir="$HOME/.${vendor[$browser]}"
	req=(browser profile vendordir)
	[[ $profile =~ anon ]] && {
		notice "Anonymous profile"
		# special case, anonymous browsing
		ztmpd
		cp -ra "$templatedir" "$ztmpdir"/"$browser"
		profilepath="$ztmpdir"/"$browser"
	}
	freq=("$profilepath")
	ckreq || return $?

	act "launching    : ${browser}.${profile}"
	act "profile path : $profilepath"
	act "preferences  : $prefs"
	func "temporary ini: ${vendordir}/profiles.ini"
	mkdir -p    "$vendordir"
	cat <<EOF > "$vendordir"/profiles.ini
[General]
StartWithLastProfile=0
[Profile0]
Name=${browser}
IsRelative=0
Path=${profilepath}
EOF

	# Inject preferences. We are lucky in browsers of the mozilla
	# family the appended settings override preceeding ones and the
	# unknown ones are ignored
	cat "$prefs" > "$profilepath"/user.js
	# special preferences for anonymous mode
	[[ $profile =~ anon ]] && {
		cat <<EOF >>    "$ztmpdir"/"$browser"/user.js
user_pref("browser.privatebrowsing.autostart", true);
EOF
	}
	# TODO: interesting to add preference locking functionality
    # see: http://kb.mozillazine.org/Locking_preferences

	### TODO: EXTENSION INJECTION IS NOT YET WELL IMPLEMENTED
	# make sure all extensions are equipped by the profile
	# mkdir -p "$profilepath"/extensions
	# cp $basedir/titlebar/titlebar-v1@dyne.org.xpi \
	#    "$profilepath"/extensions/titlebar-v1@dyne.org.xpi
	# cp $basedir/passff/bin/passff.xpi "$2"/extensions/passff-v1@dyne.org.xpi

	# [[ -r "$templatedir/extensions" ]] && {
	# 	for x in ${(f)"$(find $templatedir/extensions -name '*.xpi')"}; do
	# 		n=`basename $x`
	# 		[[ -r "$templatedir"/extensions/"$n" ]] || {
	# 			act "$n: equipping missing extension"
	# 			mkdir -p "$profilepath"/extensions/
	# 			cp "$x"  "$profilepath"/extensions/
	# 		}
	# 	done
	# }

    (sleep 5; rm "$vendordir"/profiles.ini) &

    if [[ "$firejail_profiles" = "" ]]; then
		$browser ${=browser_opts}
	else
		# generate a temporary firejail_profile to whitelist all the
		# browser specific paths
		ztmp
		cat <<EOF >> $ztmpfile
noblacklist $vendordir
noblacklist ~/.cache/${vendor[$browser]}
noblacklist $profilepath

whitelist   $vendordir
whitelist   $vendordir/profiles.ini
whitelist   ~/.cache/${vendor[$browser]}
whitelist   $profilepath

$firejail_profiles
EOF
		# find out the real binary
		local cmd_path=$(command -v $browser)
		local cmd_type=$(file $cmd_path)
		local cmd
		if [[ $cmd_type =~ symbolic ]]; then
			cmd=${cmd_type[(w)5]}
		else
			cmd=${cmd_path}
		fi

		func "firejail --profile=$ztmpfile $cmd ${=browser_opts}"
		firejail --profile="$ztmpfile" $cmd ${=browser_opts}
	fi

}

# }}}

# {{{ Misc functions

function help_usage() {
	fn help_usage
	act "Usage: tinfoil browser profile_name"
}


## TODO: fix this using pidof and locking inside dirs
function browser_is_running() {
	fn browser_is_running
	req=(browser)
	ckreq || return $?

	local brgrep="[${browser[1]}]$(print $browser | cut -c2-)"
	func "checking ps ax for '$brgrep'"

	pgrep $browser
	[[ $? = 0 ]] && {
		act "$browser found already running, using -no-remote"
		browser_opts+=(-no-remote)		
	}
}

# }}}

# {{{ Pathfinders

## looks for a file in all tinfoil directories giving precedence to
## user customisations in $homedir
function cascade_find() {
	fn cascade_find $*
	local file="$1"
	req=(file)
	ckreq || return $?
	found=""
	[[ -r "${here}/${file}" ]] && {
		found="${here}/${file}"
		return 0
	}
	[[ -r "${homedir}/${file}" ]] && {
		found="${homedir}/${file}"
		return 0
	}
	[[ -r "${confdir}/${file}" ]] && {
		found="${confdir}/${file}"
		return 0
	}
	[[ -r "${basedir}/${file}" ]] && {
		found="${basedir}/${file}"
		return 0
	}
}

function template_find() {
	fn template_find
	req=(browser profile)
	ckreq || return $?

	cascade_find "templates/${browser}.${profile}" && {
	   templatedir="$found"
	   return 0
	}

	cascade_find "templates/${browser}" && {
		templatedir="$found"
		return 0
	}


	cascade_find "templates/${browsertype}" && {
		templatedir="$found"
		return 0
	}

	return 1
}

function prefs_find() {
	fn prefs_find
	req=(browsertype)
	ckreq || return $?

	case $browsertype in
		mozilla)
			[[ -r "${templatedir}/user.js" ]] && {
				prefs="${templatedir}/user.js"
				return 0
			}
			[[ -r "${basedir}/templates/mozilla-prefs.js" ]] && {
				prefs="${basedir}/templates/mozilla-prefs.js"
				return 0
			}
			[[ "$prefs" = "" ]] && return 1

			;;
		chrome)
			# TODO: chromium default preferences
			;;
	esac

	return 1
}

function firejail_find() {
	fn firejail_find
	req=(browser browsertype)
	ckreq || return $?

	firejail_profiles=""

	cascade_find "templates/${browser}.${profile}.firejail" && {
		firejail_profiles+="
include $found"
	}
	cascade_find "templates/${browser}.firejail" && {
		firejail_profiles+="
include $found"
	}
	cascade_find "templates/${browsertype}.firejail" && {
		firejail_profiles+="
include $found"
	}
	return 1
}

# }}}

# {{{ Main()

notice "Tinfoil v0.3"

[[ "$1" = "" ]] && help_usage && return 0
[[ $* =~ -h ]]  && help_usage && return 0

# TODO: proper command parsing using zuper
create_profile=0
[[ "$1" = "new" ]] && {
	create_profile=1
	shift 1
}

browser="$1"
shift 1

# support symlinks to script with tinfoil-profilename
if [[ $0 =~ tinfoil- ]]; then
	profile=$(basename $0)
	profile=${profile[(ws:-:)2]}
else
	profile="${1}"
	shift 1
fi

browser_opts=($*)

case $browser in
	chrom*)   browsertype=chrome  ;;
	firefox*) browsertype=mozilla ;;
	icecat)   browsertype=mozilla ;;
	palemoon) browsertype=mozilla ;;
	*) error "$browser : browser not known to tinfoil"
	   act "help us add support by opening an issue on: https://github.com/dyne/tinfoil/issues"
	   ;;
esac


# browser_is_running $browser

template_find || {
	error "templates not found in paths: $basedir $homedir $confdir"
	return 1
}
act "$templatedir"

prefs_find || {
	error "preferences for $browser ($browsertype) not found in $templatedir"
	return 1
}
act "$prefs"

# use firejail if found, fills in $firejail_profile
if command -v firejail > /dev/null; then
	firejail_find
	if [[ "$firejail_profiles" = "" ]]; then
		warning "firejail profiles not found"
	else
		# list the loaded profiles
		for i in ${(f)firejail_profiles}; do
			[[ $i =~ ^include ]] && act "${i[(w)2]}"
		done
	fi
else
	warning "firejail is not installed, process isolation is not available"
fi


## TODO: tor profile to detect tor on local or remote

# create a new profile
[[ $create_profile = 1 ]] && {
	[[ -d "$profiledir"/${browser}."$profile" ]] && {
		error "profile already exists: ${profiledir}/${browser}.${profile}"
		return 1
	}
	mkdir -p              "${profiledir}/${browser}.${profile}"
	cp -ra $templatedir/* "${profiledir}/${browser}.${profile}"
	act "new profile created: ${profiledir}/${browser}.${profile}"
	act "launch with: tinfoil ${browser} ${profile}"
	return 0
}

[[ $profile =~ anon ]] || {

	[[ -d "${profiledir}/${browser}.${profile}" ]] || {
		error "profile not found: ${profiledir}/${browser}.${profile}"
		error "to create a new one use: tinfoil new ${browser} ${profile}"
		[[ -r "$profiledir" ]] && {
			act "list of available profiles:"
			find $profiledir  -maxdepth 1
		}
		return 1
	}

}

launch-$browsertype
return $?

# }}}
